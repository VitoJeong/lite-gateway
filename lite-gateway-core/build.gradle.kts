
plugins {
    kotlin("jvm") version "2.1.21"
    kotlin("plugin.spring") version "2.1.21"
    kotlin("plugin.serialization") version "2.1.21" // Kotlinx Serialization í”ŒëŸ¬ê·¸ì¸ ì¶”ê°€
    // ì½”í‹€ë¦°ì—ì„œ annotation processorë¥¼ ì‹¤í–‰í•˜ê¸° ìœ„í•œ í”ŒëŸ¬ê·¸ì¸
    // -> ë™ì ìœ¼ë¡œ annotation processorë¥¼ ì‹¤í–‰í•˜ê³ , ìƒì„±ëœ ì½”ë“œë¥¼ ì»´íŒŒì¼í•  ìˆ˜ ìˆë‹¤.
    kotlin("kapt") version "2.1.21"

    id("org.springframework.boot") version "3.3.12"
    id("io.spring.dependency-management") version "1.1.7"
}

group = "dev.jazzybyte"
version = "0.0.1-SNAPSHOT"

configurations {
    compileOnly {
        extendsFrom(configurations.annotationProcessor.get())
    }
    all {
        exclude(group = "ch.qos.logback", module = "logback-classic")
        // log4j-slf4j2-impl ëª¨ë“ˆì„ ì‚¬ìš©í•˜ê¸° ë•Œë¬¸ì— log4j-to-slf4j ëª¨ë“ˆì€ ì œì™¸
        exclude(group = "org.apache.logging.log4j", module = "log4j-to-slf4j")
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation("org.springframework.boot:spring-boot-starter-validation")
    // Kotlinx Serialization ì˜ì¡´ì„± ì¶”ê°€(Jackson ëŒ€ì²´)
    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.3")
    implementation("io.projectreactor.kotlin:reactor-kotlin-extensions:1.2.3")

    // ë¡œê¹… ê´€ë ¨ ì˜ì¡´ì„±
    implementation("io.github.oshai:kotlin-logging-jvm:5.1.1")
    implementation("org.apache.logging.log4j:log4j-api:2.20.0")
    implementation("org.apache.logging.log4j:log4j-core:2.20.0")
    implementation("org.apache.logging.log4j:log4j-slf4j2-impl:2.20.0")
    implementation("io.github.classgraph:classgraph:4.8.179")

    testImplementation("org.jetbrains.kotlin:kotlin-test-junit5")
    testImplementation("org.springframework.boot:spring-boot-starter-test")
    testImplementation("io.projectreactor:reactor-test")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
    // KotlinFixture (data class ìë™ ìƒì„±)
    testImplementation("com.appmattus.fixture:fixture:1.2.0")
    // MockK (mocking ë„êµ¬)
    testImplementation("io.mockk:mockk:1.13.17")
    // Kotlin reflection (Fixture ë‚´ë¶€ì—ì„œ í•„ìš”í•  ìˆ˜ ìˆìŒ)
    testImplementation("org.jetbrains.kotlin:kotlin-reflect")

}

sourceSets {
    test {
        java.srcDirs("src/test/kotlin")
    }
}

kotlin {
    jvmToolchain {
        languageVersion.set(JavaLanguageVersion.of(17)) // ğŸ’¡ ì—¬ê¸°
    }
    compilerOptions {
        freeCompilerArgs.addAll("-Xjsr305=strict")
    }
}

tasks.withType<Test> {
    useJUnitPlatform()
}
